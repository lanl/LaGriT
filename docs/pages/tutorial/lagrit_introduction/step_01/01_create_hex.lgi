# Tutorial Example 1
#

# Create an empty hex mesh object called 3dmesh
cmo / create / 3dmesh / / / hex

# View the mesh object attributes
cmo / status / 3dmesh

# Define parameters that will be used to create a mesh.
# Values used will be the last written

# Mesh domain 100x50x80
define / XMIN / 0.
define / XMAX / 100.
define / YMIN / 0.
define / YMAX / 50.
define / ZMIN / 0.
define / ZMAX / 80.

# Set the number of points along each axis 
# Spacing of 10 will have 11 points for length 100 
define / NX / 11
define / NY / 6
define / NZ / 9
define / NZ / 17

# Create points and connect into a hex mesh 
createpts/brick/xyz/ NX NY NZ / XMIN YMIN ZMIN/ XMAX YMAX ZMAX / 1,1,1

# Set materials for nodes (imt) and elements (itetlcr) to 1
# This is good practice and avoids 0 values in these atttributes
# Note 1,0,0 represents all for node start,stride,end 
cmo / setatt / 3dmesh / imt / 1,0,0 / 1
cmo / setatt / 3dmesh / itetclr / 1,0,0 / 1

# Set node boundary flags in the itp attribute
# This is good practice and can be called anytime to update this array
resetpts/itp

# View Mesh Object status, brief version 
cmo/status/ 3dmesh / brief

# Report the min max values of the mesh attributes
# keyword -all- or -xyz- can be used
cmo/printatt/3dmesh/ -xyz- minmax

# Check mesh with quality command
# There should be no negative or zero volumes
quality

# Write an AVS format mesh file for viewing
dump/ avs / 01_hex_mesh.inp / 3dmesh

# END of command file
finish
