# test stack into prism and write exodus files
# include method to create facesets for multi material

define / Z1 / 0.1
define / Z2 / 0.2

###################################
# One Prism

read / avs / input_one_tri.inp / mo1
cmo / setatt / mo1 / zic / 1 0 0 / Z1
dump / avs / tmp_one_tri_z1.inp / mo1 / 1 1 0 0
cmo / setatt / mo1 / zic / 1 0 0 / 0.0
dump / avs / tmp_one_tri_z0.inp / mo1 / 1 1 0 0

cmo / create / mostack / / / tri
stack / layers / avs / tmp_one_tri_z0.inp 1 tmp_one_tri_z1.inp
stack / fill / moprism / mostack
resetpts / itp
quality

dump / gmv / out_prism_one.gmv / moprism
dump / avs / out_prism_one.inp / moprism
dump / exo / out_prism_one.exo / moprism

cmo / delete / mo1
cmo / delete / mostack
cmo / delete / moprism

###################################
# One Prism Layer
#   xy min/max 0:1, z min/max 0:0.1
#   200 prisms

cmo / create / mo1 / / / triplane
createpts / xyz / 11 11 1 / 0. 0. 0. / 1. 1. 0. / 1 1 1
connect

cmo / setatt / mo1 / zic / 1 0 0 / Z1
dump / avs / tmp_one_tri_z1.inp / mo1 / 1 1 0 0
cmo / setatt / mo1 / zic / 1 0 0 / 0.0
dump / avs / tmp_one_tri_z0.inp / mo1 / 1 1 0 0

cmo / create / mostack / / / tri
stack / layers / avs / tmp_one_tri_z0.inp 1 tmp_one_tri_z1.inp
stack / fill / moprism / mostack
resetpts / itp
quality

dump / gmv / out_prism_one_layer.gmv / moprism
dump / avs / out_prism_one_layer.inp / moprism
dump / exo / out_prism_one_layer.exo / moprism

cmo / delete / mo1
cmo / delete / mostack
cmo / delete / moprism

###################################
# Two Prism Layers
#   same as above but 2 layers thick, z min/max 0:0.2
#   bottom layer 200 prisms material 1
#   top layer 200 prisms mater 2 

cmo / create / mo1 / / / triplane
createpts / xyz / 11 11 1 / 0. 0. 0. / 1. 1. 0. / 1 1 1
connect

cmo / setatt / mo1 / zic / 1 0 0 / Z2
cmo / setatt / mo1 / itetclr / 1 0 0 / 1
dump / avs / tmp_one_tri_z2.inp / mo1 / 1 1 0 0
cmo / setatt / mo1 / zic / 1 0 0 / Z1
cmo / setatt / mo1 / itetclr / 1 0 0 / 1
dump / avs / tmp_one_tri_z1.inp / mo1 / 1 1 0 0
cmo / setatt / mo1 / zic / 1 0 0 / 0.0
cmo / setatt / mo1 / itetclr / 1 0 0 / 1
dump / avs / tmp_one_tri_z0.inp / mo1 / 1 1 0 0

cmo / create / mostack / / / tri
stack / layers / avs / tmp_one_tri_z0.inp 1 tmp_one_tri_z1.inp &
2 tmp_one_tri_z2.inp
stack / fill / moprism / mostack
resetpts / itp
# quality

dump / gmv / out_prism_two_layer.gmv / moprism
dump / avs / out_prism_two_layer.inp / moprism
dump / exo / out_prism_two_layer.exo / moprism

cmo / delete / mo1
cmo / delete / mostack
cmo / delete / moprism

###################################
# One and Two Prism Layers with Perturb Elevations
#   random perturbation of z coordinate so one layer is not flat 

cmo / create / mo1 / / / triplane
createpts / xyz / 11 11 1 / 0. 0. 0. / 1. 1. 0. / 1 1 1
connect

cmo / setatt / mo1 / zic / 1 0 0 / Z2
cmo / setatt / mo1 / itetclr / 1 0 0 / 1
dump / avs / tmp_one_tri_z2.inp / mo1 / 1 1 0 0
cmo / setatt / mo1 / zic / 1 0 0 / Z1
perturb / 1 0 0 / 0. 0. 0.03
cmo / setatt / mo1 / itetclr / 1 0 0 / 1
dump / avs / tmp_one_tri_z1.inp / mo1 / 1 1 0 0
cmo / setatt / mo1 / zic / 1 0 0 / 0.0
cmo / setatt / mo1 / itetclr / 1 0 0 / 1
dump / avs / tmp_one_tri_z0.inp / mo1 / 1 1 0 0

cmo / create / mostack / / / tri
stack / layers / avs / tmp_one_tri_z0.inp 1 tmp_one_tri_z1.inp &
2 tmp_one_tri_z2.inp
stack / fill / moprism / mostack
resetpts / itp

dump / gmv / out_prism_two_layer_perturb_z.gmv / moprism
dump / avs / out_prism_two_layer_perturb_z.inp / moprism
dump / exo / out_prism_two_layer_perturb_z.exo / moprism

#
# Create a one layer versino of stacked mesh
# rmmat 2 element
# rmpoint / compress
# resetpts / itp
# dump / gmv / prism_one_layer_perturb_z.gmv / moprism
# dump / avs / prism_one_layer_perturb_z.inp / moprism
# dump / exo / prism_one_layer_perturb_z.exo / moprism

cmo / delete / mo1
cmo / delete / mostack

###################################
# Create facesets for each of 6 mesh directions

cmo / select / moprism
cmo / printatt / moprism / -all- minmax

# Extract outside faces of mesh, these will have tri on the
# top and bottom and quads on the sides
# Attributes are created during extract including
# idelem1 and idface1 which are written to faceset files 

extract / surfmesh / 1 0 0 / mo_surf / moprism / external

# Set surface elements to direction they face based on normals
cmo / addatt / mo_surf / id_side / vint / scalar / nelements
cmo / select mo_surf
settets / normal
cmo / copyatt / mo_surf mo_surf / id_side itetclr
  dump gmv out_prism_outside_faces.gmv mo_surf 
  dump avs2 out_prism_outside_faces.inp mo_surf 

# Remove all attributes except idelem1 and idface1
cmo / printatt / mo_surf / -all- / minmax
cmo / DELATT / mo_surf / itetclr0
cmo / DELATT / mo_surf / itetclr1
cmo / DELATT / mo_surf / idnode0
cmo / DELATT / mo_surf / idelem0
cmo / DELATT / mo_surf / facecol
cmo / DELATT / mo_surf / idface0
cmo / DELATT / mo_surf / layertyp

# foreach direction create cell set and write file
# use defines in case directions are permuted for user
define NUM_BOT 1
define FILE_FS_1  out_fs_1_bottom.faceset
define FILE_GMV_1 out_fs_1_bottom.gmv
define NUM_TOP 2
define FILE_FS_2  out_fs_2_top.faceset
define FILE_GMV_2 out_fs_2_top.gmv
define NUM_RIGHT 3
define FILE_FS_3  out_fs_3_right.faceset
define FILE_GMV_3 out_fs_3_right.gmv
define NUM_BACK 4
define FILE_FS_4  out_fs_4_back.faceset
define FILE_GMV_4 out_fs_4_back.gmv
define NUM_LEFT 5
define FILE_FS_5  out_fs_5_left.faceset
define FILE_GMV_5 out_fs_5_left.gmv
define NUM_FRONT 6
define FILE_FS_6  out_fs_6_front.faceset
define FILE_GMV_6 out_fs_6_front.gmv
 
# give relevant eltset names so sets are reported
# with recognizable set names for checking 
# Note with stacked layers, 
# top and bottom have equal number of elements

cmo/copy / mo_tmp / mo_surf
cmo/select / mo_tmp
  eltset/ e_bottom / id_side / eq / NUM_BOT
  eltset/ e_delete / not / e_bottom
  rmpoint/element / eltset get e_delete
  rmpoint/compress
  cmo/DELATT/mo_tmp/id_side
  dump/ avs2 / FILE_FS_1  / mo_tmp / 0 0 0 2
  dump/ gmv  / FILE_GMV_1 / mo_tmp
  cmo/delete / mo_tmp

cmo/copy / mo_tmp / mo_surf
cmo/select / mo_tmp
  eltset/ e_top / id_side / eq / NUM_TOP
  eltset/ e_delete / not / e_top
  rmpoint/element / eltset get e_delete
  rmpoint/compress
  cmo/DELATT/mo_tmp/id_side
  dump/ avs2 / FILE_FS_2  / mo_tmp / 0 0 0 2
  dump/ gmv  / FILE_GMV_2 / mo_tmp
  cmo/delete / mo_tmp

cmo/copy / mo_tmp / mo_surf
cmo/select / mo_tmp
  eltset/ e_right / id_side / eq / NUM_RIGHT
  eltset/ e_delete / not / e_right
  rmpoint/element / eltset get e_delete
  rmpoint/compress
  cmo/DELATT/mo_tmp/id_side
  dump/ avs2 / FILE_FS_3  / mo_tmp / 0 0 0 2
  dump/ gmv  / FILE_GMV_3 / mo_tmp
  cmo/delete / mo_tmp

cmo/copy / mo_tmp / mo_surf
cmo/select / mo_tmp
  eltset/ e_back / id_side / eq / NUM_BACK
  eltset/ e_delete / not / e_back
  rmpoint/element / eltset get e_delete
  rmpoint/compress
  cmo/DELATT/mo_tmp/id_side
  dump/ avs2 / FILE_FS_4  / mo_tmp / 0 0 0 2
  dump/ gmv  / FILE_GMV_4 / mo_tmp
  cmo/delete / mo_tmp

cmo/copy / mo_tmp / mo_surf
cmo/select / mo_tmp
  eltset/ e_left / id_side / eq / NUM_LEFT
  eltset/ e_delete / not / e_left
  rmpoint/element / eltset get e_delete
  rmpoint/compress
  cmo/DELATT/mo_tmp/id_side
  dump/ avs2 / FILE_FS_5  / mo_tmp / 0 0 0 2
  dump/ gmv  / FILE_GMV_5 / mo_tmp
  cmo/delete / mo_tmp

cmo/copy / mo_tmp / mo_surf
cmo/select / mo_tmp
  eltset/ e_front / id_side / eq / NUM_FRONT
  eltset/ e_delete / not / e_front
  rmpoint/element / eltset get e_delete
  rmpoint/compress
  cmo/DELATT/mo_tmp/id_side
  dump/ avs2 / FILE_FS_6  / mo_tmp / 0 0 0 2
  dump/ gmv  / FILE_GMV_6 / mo_tmp
  cmo/delete / mo_tmp

# After faceset files have been written, write the final files
# Note the file order will detirmine integer value of set in exo
# so have set 1 first, set 2 second, ...

dump / exo / out_mesh_fsets.exo / moprism / / / facesets &
  FILE_FS_1 FILE_FS_2 FILE_FS_3 &
  FILE_FS_4 FILE_FS_5 FILE_FS_6

dump / avs2 / out_mesh_fsets.inp / moprism
dump / gmv /  out_mesh_fsets.gmv / moprism

# Convert binary mesh_fsets.exo with ncdump executable:
# ncdump mesh_fsets.exo > mesh_fsets_ascii.exo

# Creat single material mesh 

cmo setatt moprism itetclr 1
cmo setatt moprism imt 1
resetpts itp

dump / exo / out_mesh_fsets_single_mat.exo / moprism / / / facesets &
  FILE_FS_1 FILE_FS_2 FILE_FS_3 &
  FILE_FS_4 FILE_FS_5 FILE_FS_6

dump / avs2 / out_mesh_fsets_single_mat.inp / moprism
dump / gmv /  out_mesh_fsets_single_mat.gmv / moprism


finish
